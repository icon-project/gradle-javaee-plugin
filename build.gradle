plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.22.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

def snapshotSuffix = project.hasProperty('release') ? '' : '-SNAPSHOT'
group = GROUP
version = VERSION + snapshotSuffix

gradlePlugin {
    automatedPublishing = false
    plugins {
        javaeePlugin {
            id = 'foundation.icon.javaee'
            implementationClass = 'foundation.icon.gradle.plugins.javaee.JavaeePlugin'
        }
    }
}

dependencies {
    implementation 'foundation.icon:javaee-tooling:0.8.7'
    implementation 'foundation.icon:icon-sdk:2.0.0-SNAPSHOT'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

def repoUrl = 'https://github.com/icon-project/gradle-javaee-plugin'
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "iconfoundation"
            name "icon.foundation"
            email "foo@icon.foundation"
        }
    }
    scm {
        url repoUrl
    }
}

publishing {
    repositories {
        maven {
            name = 'mavenCentral'
            def releasesUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = project.hasProperty('mavenCentralUsername') ? "$mavenCentralUsername" : ''
                password = project.hasProperty('mavenCentralPassword') ? "$mavenCentralPassword" : ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', 'Gradle plugin for ICON JavaEE')
                root.appendNode('url', repoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusStaging {
    packageGroup = GROUP
    stagingProfileId = project.hasProperty('mavenProfileId') ? "$mavenProfileId" : ''
    username = project.hasProperty('mavenCentralUsername') ? "$mavenCentralUsername" : ''
    password = project.hasProperty('mavenCentralPassword') ? "$mavenCentralPassword" : ''
}
