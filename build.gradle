plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

repositories {
    jcenter()
    maven {
        url "http://ci.arch.iconloop.com/nexus/repository/maven-public"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

def snapshotSuffix = rootProject.hasProperty('release') ? '' : '-SNAPSHOT'
group = 'foundation.icon'
version = '0.5.1' + snapshotSuffix

gradlePlugin {
    automatedPublishing = false
    plugins {
        javaeeSdk {
            id = 'foundation.icon.javaee-sdk'
            implementationClass = 'foundation.icon.gradle.plugins.javaee.SDKPlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            def releasesUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-releases"
            def snapshotsUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = rootProject.hasProperty('nexusUsername') ? "$nexusUsername" : ''
                password = rootProject.hasProperty('nexusPassword') ? "$nexusPassword" : ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    implementation 'foundation.icon:javaee-tooling:0.8.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

test {
    useJUnitPlatform()
}
