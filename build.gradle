plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

repositories {
    jcenter()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

def snapshotSuffix = rootProject.hasProperty('release') ? '' : '-SNAPSHOT'
group = 'foundation.icon'
version = '0.3.0' + snapshotSuffix

gradlePlugin {
    automatedPublishing = false
    plugins {
        javaeeSdk {
            id = 'foundation.icon.javaee-sdk'
            implementationClass = 'foundation.icon.gradle.plugins.javaee.SDKPlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            def releasesUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-releases"
            def snapshotsUrl = "http://ci.arch.iconloop.com/nexus/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsUrl : releasesUrl
            credentials {
                username = rootProject.hasProperty('nexusUsername') ? "$nexusUsername" : ''
                password = rootProject.hasProperty('nexusPassword') ? "$nexusPassword" : ''
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.msgpack:msgpack-core:0.8.17'
    implementation 'org.ow2.asm:asm:7.0'
    implementation 'org.ow2.asm:asm-commons:7.0'
    implementation 'org.ow2.asm:asm-tree:7.0'
    implementation 'org.ow2.asm:asm-util:7.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

test {
    useJUnitPlatform()
}
